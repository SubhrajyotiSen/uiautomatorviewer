plugins {
    id "java"
    id 'application'
    id "com.diffplug.eclipse.mavencentral" version "3.33.3"
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'com.android.tools'
archivesBaseName = 'uiautomatorviewer'

repositories {
    mavenCentral()
    google()
}

application {
    mainClass = 'com.android.uiautomator.UiAutomatorViewer'
}

eclipseMavenCentral {
    release '4.21.0', {
        // supports the standard java configurations
        implementation 'org.eclipse.swt'
        // specify this to add the native jars for this platform
        useNativesForRunningPlatform()
        // specify that all transitive dependencies should be from this release
        // (otherwise the version ranges which eclipse uses will use latest available)
        constrainTransitivesToThisRelease()
    }
}

dependencies {
    implementation 'com.android.tools.ddms:ddmlib:30.0.4'
    implementation 'org.eclipse.platform:org.eclipse.jface:3.24.0'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.android.tools:common:27.0.2'
}

sourceSets {
    main.resources.srcDir 'src/main/java'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

shadowJar {
    minimize() {
        exclude dependency('org.eclipse.platform:.*:.*')
    }
}

